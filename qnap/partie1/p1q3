
/CONTROL/
OPTION = NSOURCE;
TMAX = 100000;
ACCURACY = ALL QUEUE;


/DECLARE/
QUEUE SRC,LIEN_H, LIEN_B_1, LIEN_B_2;
INTEGER nbTotal, nbTransmis, nbRejets,erlang,I,J,capacite,rho;
REAL somme,DEBIT,lambda,mu,pRejets,X, pourcentage;
CUSTOMER REAL duree;
FILE fichier;


/STATION/
NAME = SRC;
TYPE = SOURCE;
SERVICE =  BEGIN
    duree := ( 1. / mu );
    nbTotal := nbTotal + 1;
    EXP( 1. / lambda );
END;
TRANSIT = LIEN_H;

/STATION/
NAME = LIEN_H;
TYPE = MULTIPLE(capacite);
SERVICE =  BEGIN
    CST(duree);
    TRANSIT(OUT);
END;
CAPACITY = capacite;

REJECT = BEGIN
    nbRejets := nbRejets + 1;
    TRANSIT(LIEN_B_1);
END;


/STATION/
NAME = LIEN_B_1;
TYPE = MULTIPLE(capacite);
SERVICE = BEGIN
    CST(duree);
    TRANSIT(LIEN_B_2);
END;
CAPACITY = capacite;
REJECT = BEGIN
    nbRejets := nbRejets + 1;
    TRANSIT(OUT);
END;

/STATION/
NAME = LIEN_B_2;
TYPE = MULTIPLE(capacite);
SERVICE = BEGIN
    CST(duree);
    TRANSIT(OUT);
END;
CAPACITY = capacite;
REJECT = BEGIN
    nbRejets := nbRejets + 1;
    TRANSIT(OUT);
END;


/EXEC/
    BEGIN
        FILASSIGN(fichier , "p1.q3.data");
        OPEN(fichier);

        DEBIT := 64000;
        mu := 1. / 180;
        erlang := 70;

        FOR rho := 1 STEP 1 UNTIL erlang DO
        BEGIN
            FOR capacite := rho STEP 1 UNTIL 2 * rho DO
                BEGIN
                    lambda := mu * rho;

                    nbTotal := 0;
                    nbRejets := 0;
                    nbTransmis := 0;

                    SIMUL;

                    pourcentage := nbRejets * 1. / nbTotal;

                    WRITELN(fichier, rho , " " , capacite , " " , nbTotal , " " , nbRejets , " " , pourcentage );
                END;
        END;
    END;
/END/

