
&
/DECLARE/

QUEUE SRC, Q;

REAL LAMBDA, RO;
REAL MU = 1000;
INTEGER I;

QUEUE fakeOUT;

REAL temps;

REAL minA;
REAL maxA;
REAL sumA;

REAL minT;
REAL maxT;
REAL sumT;

CUSTOMER INTEGER nbpsg;
CUSTOMER REAL Tdebut;

FILE cbQ;


&
/STATION/

NAME = SRC;
TYPE = SOURCE;
SERVICE = BEGIN
    EXP(1. / LAMBDA);
    Tdebut := TIME;
END;
TRANSIT = Q;


&
/STATION/

NAME = Q;
SERVICE = BEGIN
    EXP(1. / MU);
END;
TRANSIT = fakeOUT;


&
/STATION/

NAME = fakeOUT;
SERVICE = BEGIN
    temps := (TIME - Tdebut);

    sumT := sumT + temps;

    IF minT > temps THEN
    BEGIN
        minT := temps;
    END;

    IF maxT < temps THEN
    BEGIN
        maxT := temps;
    END;

    sumA := sumA + CUSTNB(Q);

    IF minA > CUSTNB(Q) THEN
    BEGIN
        minA := CUSTNB(Q);
    END;

    IF maxA < CUSTNB(Q) THEN
    BEGIN
        maxA := CUSTNB(Q);
    END;
END;
TRANSIT = OUT;


&
/CONTROL/

TMAX = 1000;


&
/EXEC/

BEGIN
    & On fait varier Rho pour qu'il prenne 99 valeurs définies et qu'on réalise nos mesures (0.01 , ... , 0.99)
    FILASSIGN(cbQ,"p2.q3.data");
    OPEN(cbQ);
    & boucle des simulations
    FOR I := 1 STEP 1 UNTIL 99 DO
    BEGIN
        & initialisation des variables
        RO := I * 0.010;
        LAMBDA := RO * MU;
        minA := 10000000;
        maxA := 0;
        sumA := 0;
        minT := 10000000;
        maxT := 0;
        sumT := 0;
        & simulation
        SIMUL;
        & affichage de la progression
        WRITELN(I);
        & ecriture resultats dans le fichier
        WRITELN(cbQ,I," ",minT," ",maxT," ",sumT/SERVNB(fakeOUT)," ",minA," ",maxA," ",sumA/SERVNB(fakeOUT));
    END;
END;


&
/END/


&
& GNUPLOT
&
& plot "p2.q3.data" u 1:2 t "minT", "p2.q3.data" u 1:3 t "maxT", "p2.q3.data" u 1:4 w l t "moyenne temps traitement", "p2.q3.data" u 1:5 t "minA" axes x1y2, "p2.q3.data" u 1:6 t "maxA" axes x1y2, "p2.q3.data" u 1:7 w l t "mayyenne nb packet att" axes x1y2
&
